*-----------------------------------------------------------------------------
* <Rating>-60</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE EOD.SYND.UPFRNT.PROFIT.DIST(ISH.ID)

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.USER
    $INSERT I_F.ACCOUNT
    $INSERT I_F.CUSTOMER
    $INSERT I_F.CURRENCY
    $INSERT I_F.IS.H.CONTRACTS
    $INSERT I_F.IS.H.PARAMETER
    $INSERT I_F.STMT.ENTRY
    $INSERT I_F.CATEG.ENTRY
    $INSERT I_F.LD.LOANS.AND.DEPOSITS
    $INSERT I_F.LMM.ACCOUNT.BALANCES
    $INSERT I_F.LMM.SCHEDULES.PAST

    $INSERT I_EOD.SYND.UPFRNT.PFT.DIST.COMMON

    GOSUB INIT.OPEN
    GOSUB PROCESS

    RETURN

INIT.OPEN:

    R.IS.H.CONTRACTS = ''
    R.IS.H.PARAMETER = ''
    LD.ID = '' ; Y.CURRENCY = ''
    Y.PROD.CODE = ''

    RETURN

PROCESS:

    CALL F.READ(FN.IS.H.CONTRACTS,ISH.ID,R.IS.H.CONTRACTS,F.IS.H.CONTRACTS,ERR.IS.H.CONTRACTS)
    Y.CURRENCY = R.IS.H.CONTRACTS<IS.CON.CURRENCY>
    Y.PROD.CODE = R.IS.H.CONTRACTS<IS.CON.PROD.CODE>
    CALL F.READ(FN.IS.H.PARAMETER,Y.PROD.CODE,R.IS.H.PARAMETER,F.IS.H.PARAMETER,ERR.IS.H.PARAMETER)
    Y.SYND.CATEG = R.IS.H.PARAMETER<IS.PARM.SYND.MUS.CATEG>
    Y.MUSH.PL.CATEG = R.IS.H.PARAMETER<IS.PARM.MUSH.PL.CATEG>
    Y.DR.CODE = R.IS.H.PARAMETER<IS.PARM.MUSH.DR.CODE>
    Y.CR.CODE = R.IS.H.PARAMETER<IS.PARM.MUSH.CR.CODE>

    SYND.CUS.LIST = R.IS.H.CONTRACTS<IS.CON.SYND.CUST.ID>
    SYND.ACC.LIST = R.IS.H.CONTRACTS<IS.CON.SYND.CUST.ACC>
    SYND.MUS.PCT.LIST = R.IS.H.CONTRACTS<IS.CON.SYND.CUS.MUS.PCT>
    SYND.UPFRNT.PCT.LIST = R.IS.H.CONTRACTS<IS.CON.SYND.UPFRNT.PRFT.PCT>
    SYND.UPFRNT.DATE.LIST = R.IS.H.CONTRACTS<IS.CON.SYND.PRFT.PAY.DATE>

    CONVERT VM TO FM IN SYND.CUS.LIST
    CONVERT VM TO FM IN SYND.ACC.LIST
    CONVERT VM TO FM IN SYND.MUS.PCT.LIST
    CONVERT VM TO FM IN SYND.UPFRNT.PCT.LIST
    CONVERT VM TO FM IN SYND.UPFRNT.DATE.LIST

    COUNT.CUS = DCOUNT(SYND.CUS.LIST,FM)
    FOR K = 1 TO COUNT.CUS

        Y.UPFRNT.DATE = SYND.UPFRNT.DATE.LIST<K>
        IF Y.UPFRNT.DATE EQ TODAY OR ( Y.UPFRNT.DATE LT Y.NEXT.WORKING.DAY AND Y.UPFRNT.DATE GT TODAY ) THEN
            SYND.CUS.ID = SYND.CUS.LIST<K>
            Y.CUS.ACCOUNT = SYND.ACC.LIST<K>
            Y.MUSH.ACCOUNT = Y.CURRENCY : Y.SYND.CATEG : "0001"
            Y.MUS.PERC = SYND.MUS.PCT.LIST<K>
            Y.UPFRNT.PRFT.PERC = SYND.UPFRNT.PCT.LIST<K>

            Y.MUS.AMT = R.IS.H.CONTRACTS<IS.CON.NET.COST> * Y.MUS.PERC/100
            Y.AMT = Y.MUS.AMT * Y.UPFRNT.PRFT.PERC/100
            GOSUB COMMON.ACCOUNTING.ARRAY
            GOSUB DEBIT.PL.CATEG
            GOSUB CREDIT.CUST
            GOSUB RAISE.ACCOUNTING
        END

    NEXT K

    RETURN

COMMON.ACCOUNTING.ARRAY:

    Y.EB.COMM.ARR<AC.STE.COMPANY.CODE> = ID.COMPANY
    Y.EB.COMM.ARR<AC.STE.TRANS.REFERENCE> = ISH.ID
    Y.EB.COMM.ARR<AC.STE.VALUE.DATE> = TODAY
    Y.EB.COMM.ARR<AC.STE.BOOKING.DATE> = TODAY
    Y.EB.COMM.ARR<AC.STE.SYSTEM.ID> = 'IS'
    Y.EB.COMM.ARR<AC.STE.CURRENCY.MARKET> = '1'
    Y.EB.COMM.ARR<AC.STE.CURRENCY> = Y.CURRENCY
    Y.EB.COMM.ARR<AC.STE.OUR.REFERENCE> = ISH.ID
    Y.EB.COMM.ARR<AC.STE.DEPARTMENT.CODE> = Y.ACCOUNT.OFFICER
    Y.EB.COMM.ARR<AC.STE.ACCOUNT.OFFICER> = Y.ACCOUNT.OFFICER

    RETURN

DEBIT.PL.CATEG:

    Y.DR.PL.ARR = Y.EB.COMM.ARR

    IF Y.CURRENCY EQ LCCY THEN
        Y.DR.PL.ARR<AC.STE.AMOUNT.LCY> = Y.AMT * -1
    END ELSE
        Y.DR.PL.ARR<AC.STE.AMOUNT.FCY> = Y.AMT * -1
    END

    Y.DR.PL.ARR<AC.STE.PL.CATEGORY> = Y.MUSH.PL.CATEG
    Y.DR.PL.ARR<AC.STE.TRANSACTION.CODE> = Y.DR.CODE

    Y.ENTRY.ARR = LOWER(Y.DR.PL.ARR)

    RETURN

CREDIT.CUST:

    Y.CR.CUS.ARR = Y.EB.COMM.ARR

    IF Y.CURRENCY EQ LCCY THEN
        Y.CR.CUS.ARR<AC.STE.AMOUNT.LCY> = Y.AMT
    END ELSE
        Y.CR.CUS.ARR<AC.STE.AMOUNT.FCY> = Y.AMT
    END

    Y.CR.CUS.ARR<AC.STE.CUSTOMER.ID> = SYND.CUS.ID
    Y.CR.CUS.ARR<AC.STE.ACCOUNT.NUMBER> = Y.CUS.ACCOUNT
    CALL DBR("ACCOUNT":FM:AC.CATEGORY,Y.CUS.ACCOUNT,Y.CATEGORY)
    Y.CR.CUS.ARR<AC.STE.PRODUCT.CATEGORY> = Y.CATEGORY
    Y.CR.CUS.ARR<AC.STE.TRANSACTION.CODE> = Y.CR.CODE

    Y.ENTRY.ARR<-1> = LOWER(Y.CR.CUS.ARR)

    RETURN

RAISE.ACCOUNTING:

    V = IS.CON.AUDIT.DATE.TIME
    CALL EB.ACCOUNTING("FT","SAO",Y.ENTRY.ARR,'')

    RETURN

END
