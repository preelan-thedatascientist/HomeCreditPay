 SUBROUTINE EOD.SYND.PROFIT.DISTRIBUTION(ISH.ID)

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.USER
    $INSERT I_F.ACCOUNT
    $INSERT I_F.CUSTOMER
    $INSERT I_F.CURRENCY
    $INSERT I_F.IS.H.CONTRACTS
    $INSERT I_F.IS.H.PARAMETER
    $INSERT I_F.STMT.ENTRY
    $INSERT I_F.CATEG.ENTRY
    $INSERT I_F.LD.LOANS.AND.DEPOSITS
    $INSERT I_F.LMM.ACCOUNT.BALANCES
    $INSERT I_F.LMM.SCHEDULES.PAST

    $INSERT I_EOD.SYND.PROFIT.DIST.COMMON

    GOSUB INIT.OPEN
    GOSUB PROCESS

    RETURN

INIT.OPEN:

    R.IS.H.CONTRACTS = ''
    R.IS.H.PARAMETER = ''
    LD.ID = '' ; Y.CURRENCY = ''
    Y.PROD.CODE = ''
    Y.ENTRY.ARR = ''
    R.LMM = ''
    R.SCHED = ''
    MUSH.PFT.PAID = 0

    RETURN

PROCESS:

    CALL F.READ(FN.IS.H.CONTRACTS,ISH.ID,R.IS.H.CONTRACTS,F.IS.H.CONTRACTS,ERR.IS.H.CONTRACTS)
    PROFIT.PAY.TYPE = R.IS.H.CONTRACTS<IS.CON.MUSH.PRFT.PAY.TYPE>
    LD.ID = R.IS.H.CONTRACTS<IS.CON.SALE.LD.ID>
    Y.CURRENCY = R.IS.H.CONTRACTS<IS.CON.CURRENCY>
    Y.PROD.CODE = R.IS.H.CONTRACTS<IS.CON.PROD.CODE>
    NET.COST = R.IS.H.CONTRACTS<IS.CON.NET.COST>
    CALL F.READ(FN.IS.H.PARAMETER,Y.PROD.CODE,R.IS.H.PARAMETER,F.IS.H.PARAMETER,ERR.IS.H.PARAMETER)
    Y.MUSH.CATEG = R.IS.H.PARAMETER<IS.PARM.MUSH.CATEG>
    Y.MUSH.PL.CATEG = R.IS.H.PARAMETER<IS.PARM.MUSH.PL.CATEG>
    Y.MGMT.FEE.PCT = R.IS.H.PARAMETER<IS.PARM.MUSH.MGMT.FEE.PCT>
    Y.DR.CODE = R.IS.H.PARAMETER<IS.PARM.MUSH.DR.CODE>
    Y.CR.CODE = R.IS.H.PARAMETER<IS.PARM.MUSH.CR.CODE>

    CALL F.READ(FN.LD.LOANS.AND.DEPOSITS,LD.ID,R.LD.LOANS.AND.DEPOSITS,F.LD.LOANS.AND.DEPOSITS,ERR.LD.LOANS.AND.DEPOSITS)
    LD.MAT.DATE = R.LD.LOANS.AND.DEPOSITS<LD.FIN.MAT.DATE>

    CALL F.READ(FN.LMM.SCHEDULE.DATES,LD.ID:'00',R.SCHED,F.LMM.SCHEDULE.DATES,'')

    YDATES = COUNT(R.SCHED,FM) + 1
    FOR I = 1 TO YDATES
        GREG.DATE = ''
        JUL.DATE = R.SCHED<I,1>
        CALL JULDATE(GREG.DATE,JUL.DATE)
        IF GREG.DATE EQ TODAY OR ( GREG.DATE LT Y.NEXT.WORKING.DAY AND GREG.DATE GT Y.LAST.WORKING.DAY ) THEN
            GOSUB PROCESS.LMM
        END
    NEXT I

    RETURN

PROCESS.LMM:

    CALL F.READ(FN.LMM.ACCOUNT.BALANCES,LD.ID:'00',R.LMM,F.LMM.ACCOUNT.BALANCES,'')

    NO.OF.DATES = DCOUNT(R.LMM<LD27.DATE.INT.ACC.TO>,VM)
    FOR J = 1 TO NO.OF.DATES
        Y.DATE = R.LMM<LD27.DATE.INT.ACC.TO,J>
        IF Y.DATE LT TODAY OR Y.DATE LT GREG.DATE THEN
            TOT.ACCR.INT += ABS(R.LMM<LD27.INT.AMT.TODATE,J>)
        END
    NEXT J

    IF TOT.ACCR.INT THEN

        MUSH.CUS.LIST = R.IS.H.CONTRACTS<IS.CON.MUSH.CUST.ID>
        MUSH.ACC.LIST = R.IS.H.CONTRACTS<IS.CON.MUSH.CUST.ACC>
        MUSH.PCT.LIST = R.IS.H.CONTRACTS<IS.CON.MUSH.PCNT>
        MUSH.PRFT.PCT.LIST = R.IS.H.CONTRACTS<IS.CON.MUSH.PRFT.PCNT>

        CONVERT VM TO FM IN MUSH.CUS.LIST
        CONVERT VM TO FM IN MUSH.ACC.LIST
        CONVERT VM TO FM IN MUSH.PCT.LIST
        CONVERT VM TO FM IN MUSH.PRFT.PCT.LIST

        COUNT.CUS = DCOUNT(MUSH.CUS.LIST,FM)
        FOR K = 1 TO COUNT.CUS

            MUSH.PFT.PAID = R.IS.H.CONTRACTS<IS.CON.MUSH.PFT.PAID,K>
            MUSH.CUS.ID = MUSH.CUS.LIST<K>
            Y.CUS.ACCOUNT = MUSH.ACC.LIST<K>
            Y.MUSH.ACCOUNT = Y.CURRENCY : Y.MUSH.CATEG : "0001"
            Y.PERC = MUSH.PRFT.PCT.LIST<K>
            Y.MUSH.PCT = MUSH.PCT.LIST<K>
            IF Y.PERC EQ '' THEN Y.PERC = MUSH.PCT.LIST<K>
            Y.AMT = (TOT.ACCR.INT * Y.PERC/100) - MUSH.PFT.PAID
            Y.FEE = (Y.AMT * Y.MGMT.FEE.PCT/100)

            IF PROFIT.PAY.TYPE EQ 'AUTO' THEN
                GOSUB COMMON.ACCOUNTING.ARRAY

                GOSUB DEBIT.PL.CATEG
                GOSUB CREDIT.CUST

                GOSUB CREDIT.MGMT.FEE.CATEG
                GOSUB DEBIT.CUST

                R.IS.H.CONTRACTS<IS.CON.MUSH.PFT.PAID,K> += Y.AMT
                R.IS.H.CONTRACTS<IS.CON.MUSH.MGMT.FEE,K> += Y.FEE
            END

            R.IS.H.CONTRACTS<IS.CON.MUSH.PFT.ACCRUED,K> += Y.AMT

            IF LD.MAT.DATE EQ TODAY OR LD.MAT.DATE LT Y.NEXT.WORKING.DAY THEN
                Y.AMT = NET.COST * Y.MUSH.PCT/100
                GOSUB COMMON.ACCOUNTING.ARRAY
                GOSUB CREDIT.CUST
                GOSUB DEBIT.MUSH.ACCT
            END

        NEXT K
        GOSUB RAISE.ACCOUNTING
        CALL F.WRITE(FN.IS.H.CONTRACTS,ISH.ID,R.IS.H.CONTRACTS)
    END

    RETURN

COMMON.ACCOUNTING.ARRAY:

    Y.EB.COMM.ARR<AC.STE.COMPANY.CODE> = ID.COMPANY
    Y.EB.COMM.ARR<AC.STE.TRANS.REFERENCE> = ISH.ID
    Y.EB.COMM.ARR<AC.STE.VALUE.DATE> = TODAY
    Y.EB.COMM.ARR<AC.STE.BOOKING.DATE> = TODAY
    Y.EB.COMM.ARR<AC.STE.SYSTEM.ID> = 'IS'
    Y.EB.COMM.ARR<AC.STE.CURRENCY.MARKET> = '1'
    Y.EB.COMM.ARR<AC.STE.CURRENCY> = Y.CURRENCY
    Y.EB.COMM.ARR<AC.STE.OUR.REFERENCE> = ISH.ID
    Y.EB.COMM.ARR<AC.STE.ACCOUNT.OFFICER> = Y.ACCOUNT.OFFICER

    RETURN

DEBIT.PL.CATEG:

    Y.DR.PL.ARR = Y.EB.COMM.ARR

    IF Y.CURRENCY EQ LCCY THEN
        Y.DR.PL.ARR<AC.STE.AMOUNT.LCY> = Y.AMT * -1
    END ELSE
        Y.DR.PL.ARR<AC.STE.AMOUNT.FCY> = Y.AMT * -1
    END

    Y.DR.PL.ARR<AC.STE.PL.CATEGORY> = Y.MUSH.PL.CATEG
    Y.DR.PL.ARR<AC.STE.TRANSACTION.CODE> = Y.DR.CODE

    Y.ENTRY.ARR<-1> = LOWER(Y.DR.PL.ARR)

    RETURN

CREDIT.CUST:

    Y.CR.CUS.ARR = Y.EB.COMM.ARR

    IF Y.CURRENCY EQ LCCY THEN
        Y.CR.CUS.ARR<AC.STE.AMOUNT.LCY> = Y.AMT
    END ELSE
        Y.CR.CUS.ARR<AC.STE.AMOUNT.FCY> = Y.AMT
    END

    Y.CR.CUS.ARR<AC.STE.CUSTOMER.ID> = MUSH.CUS.ID
    Y.CR.CUS.ARR<AC.STE.ACCOUNT.NUMBER> = Y.CUS.ACCOUNT
    Y.CR.CUS.ARR<AC.STE.TRANSACTION.CODE> = Y.CR.CODE

    Y.ENTRY.ARR<-1> = LOWER(Y.CR.CUS.ARR)

    RETURN

CREDIT.MGMT.FEE.CATEG:

    Y.CR.FEE.ARR = Y.EB.COMM.ARR

    IF Y.CURRENCY EQ LCCY THEN
        Y.CR.FEE.ARR<AC.STE.AMOUNT.LCY> = Y.FEE
    END ELSE
        Y.CR.FEE.ARR<AC.STE.AMOUNT.FCY> = Y.FEE
    END

    Y.CR.FEE.ARR<AC.STE.PL.CATEGORY> = Y.MUSH.PL.CATEG
    Y.CR.FEE.ARR<AC.STE.TRANSACTION.CODE> = Y.CR.CODE

    Y.ENTRY.ARR<-1> = LOWER(Y.CR.FEE.ARR)

    RETURN

DEBIT.CUST:

    Y.DR.CUS.ARR = Y.EB.COMM.ARR

    IF Y.CURRENCY EQ LCCY THEN
        Y.DR.CUS.ARR<AC.STE.AMOUNT.LCY> = Y.FEE * -1
    END ELSE
        Y.DR.CUS.ARR<AC.STE.AMOUNT.FCY> = Y.FEE * -1
    END

    Y.DR.CUS.ARR<AC.STE.CUSTOMER.ID> = MUSH.CUS.ID
    Y.DR.CUS.ARR<AC.STE.ACCOUNT.NUMBER> = Y.CUS.ACCOUNT
    Y.DR.CUS.ARR<AC.STE.TRANSACTION.CODE> = Y.DR.CODE

    Y.ENTRY.ARR<-1> = LOWER(Y.DR.CUS.ARR)

    RETURN

DEBIT.MUSH.ACCT:

    Y.DR.MUSH.ARR = Y.EB.COMM.ARR

    IF Y.CURRENCY EQ LCCY THEN
        Y.DR.MUSH.ARR<AC.STE.AMOUNT.LCY> = (Y.AMT * -1)
    END ELSE
        Y.DR.MUSH.ARR<AC.STE.AMOUNT.FCY> = (Y.AMT * -1)
    END

    Y.DR.MUSH.ARR<AC.STE.ACCOUNT.NUMBER> = Y.MUSH.ACCOUNT
    Y.DR.MUSH.ARR<AC.STE.TRANSACTION.CODE> = Y.DR.CODE

    Y.ENTRY.ARR<-1> = LOWER(Y.DR.MUSH.ARR)

    RETURN


RAISE.ACCOUNTING:

    V = IS.CON.AUDIT.DATE.TIME
    CALL EB.ACCOUNTING("FT","SAO",Y.ENTRY.ARR,'')

    RETURN

END
